apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        abortOnError false
    }
    defaultConfig {
        applicationId "org.hermes"
        minSdkVersion 24
        targetSdkVersion 28
        versionCode 1000
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    flavorDimensions "targetAPILevel"
    productFlavors {
        min24 {
            dimension "targetAPILevel"
            versionNameSuffix "-min24api"
            minSdkVersion 24
            versionCode 24 * 10 + android.defaultConfig.versionCode
            versionName "2412001"
        }
        max23 {
            dimension "targetAPILevel"
            versionNameSuffix "-max23api"
            minSdkVersion 23
            maxSdkVersion 23
            versionCode 23 * 10 + android.defaultConfig.versionCode
            versionName "2312001"
        }
    }
}

task buildJnis(type: Exec) {
    def res = commandLine '/bin/bash', '-c',
            'git submodule update --init --recursive && ' +
                    'cd ../../entangled && ' +
                    'git status | head -n 1 | sed \'s/On branch //\''

//    For the time being we just use the commit that the submodule is pointed to
//    we are not going to be updating the repo every time we build the JNI libs.
//    if (res.toString() != 'develop') {
//        println 'Not in develop branch'
//        commandLine '/bin/bash', '-c', 'cd ../../entangled && git reset --hard && git checkout develop'
//    }
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    def sdkDir = properties.getProperty('sdk.dir', '')
    def ndkDir = properties.getProperty('ndk.dir', '')
    def cpu_archs = 'armeabi-v7a,arm64-v8a,x86,x86_64'
    def bazel_env_vars = "ANDROID_HOME=${sdkDir} ANDROID_NDK_HOME=${ndkDir}"

    println """
=============================

Android SDK Path: ${sdkDir}
Android NDK Path: ${ndkDir}
entangled repository is on branch: ${res.toString()}

=============================
"""

    commandLine '/bin/bash', '-c', 'cd ../../entangled && ' +
            "${bazel_env_vars} bazel build --fat_apk_cpu='${cpu_archs}' --copt=-Ofast //mobile/android:dummy && " +
            'rm -rf ../android-client/build/jni &&' +
            'echo A | unzip bazel-bin/mobile/android/dummy.apk -d ../android-client/build/jni && ' +
            'cp -r ../android-client/build/jni/lib/ ../android-client/app/src/main/jniLibs'
}

dependencies {
    // TODO: Split all the version numbers into the android-client/build.gradle
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.core:core-ktx:1.0.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation "androidx.lifecycle:lifecycle-extensions:$rootProject.ext.archLifecycleVersion"
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.0.0'
    implementation "androidx.paging:paging-runtime-ktx:2.1.0"
    implementation "androidx.room:room-runtime:${rootProject.ext.room_version}"
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation "com.google.dagger:dagger:2.22.1"
    implementation "com.google.dagger:dagger-android-support:2.22.1"
    implementation "com.squareup.moshi:moshi-kotlin:1.8.0"
    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'org.apache.commons:commons-lang3:3.9'
    implementation 'org.bouncycastle:bcpkix-jdk15on:1.61'
    implementation 'org.bouncycastle:bcprov-jdk15on:1.61'
    implementation "org.jetbrains.kotlin:kotlin-reflect:${rootProject.ext.kotlin_version}"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${rootProject.ext.kotlin_version}"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines:0.19.2'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.1.1'
    implementation 'org.threeten:threetenbp:1.3.8'
    kapt "androidx.room:room-compiler:${rootProject.ext.room_version}"
    kapt "com.google.dagger:dagger-android-support:2.22.1"
    kapt "com.google.dagger:dagger-android-processor:2.22.1"
    kapt "com.google.dagger:dagger-compiler:2.22.1"
    max23Api files('libs/jota-android-legacy-1.0.0-beta5.jar')
    min24Api 'com.github.iotaledger:iota-java:1.0.0-beta5'
    testImplementation "androidx.arch.core:core-testing:2.0.1"
    testImplementation "androidx.test.espresso:espresso-core:3.2.0-alpha05"
    testImplementation "androidx.test.ext:junit:1.1.0"
    testImplementation "androidx.test:runner:1.2.0-alpha05"
    testImplementation 'it.ozimov:assertj:2.0.0'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.4.0'
}
